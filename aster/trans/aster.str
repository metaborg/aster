/**
 * Aster main module.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module aster

imports
  libstratego-lib
  
  lib/xtc-multi-options

  io
  options
  aster/lang/debug
  back/assimilate
  front/analyze
  front/check
  front/desugar
  front/include-library

strategies

  // TODO: Add switch for run-time checks, e.g., for circularity and tree concistency
  
  aster =
    init-shared-vars;
    one(aster-include-library);
    desugar-all;
    
    // init-check; // TODO: init-check, if working w/ dyn. rules...?
    all-stratego-defs(try(analyze));
    where(check-all);
    assimilate-all

strategies
 
  init-shared-vars =
    // TODO2: Things like <all-parents> should be optimized in a post-processing stage, not right away
    // TODO2: Rename global-cache to cache or store, or something
    rules(
      ParentVar           := <newname> "parent"
      NodeVar             := <newname> "node"
      InputVar            := <newname> "input"
      AllParentsVar       := <newname> "all-parents"
      GlobalCacheVar      := <newname> "global-cache"
      RewriteModeVar      := <newname> "rewrite-mode"
      RootVar             := <newname> "root"
      CachedVar           := <newname> "cached"
      DecoratedValueVar   := <newname> "decorated-value-cache"
      SignatureVar        := <newname> "signature"
      AlteredSignatureVar := <newname> "altered-signature"
    )

strategies // I/O

  main-aster =
    ?args;
    xtc-multi-io-wrap(
      aster-options
    ,
      log-timed-main(
        with(<infer-output-option> args);
        map(input-stratego-file);
        aster;
        map(output-stratego-file)
      )
    )
  
  log-timed-main(s) =
     with(
       <get-config> "--statistics"
     <+
       <set-config> ("--statistics", 0)
     );
     log-timed(
       s
     <+
       fatal-err-msg(|<conc-strings> ("Compilation failed (", <run-time ; real-to-string(|2)>, "s)"))
     |"compilation succeeded", 0
     );
     with(
       <set-config> ("--statistics", 0) // already displayed success and time
     )
