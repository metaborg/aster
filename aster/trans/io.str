module io

imports
  libstratego-lib
  libstratego-sglr
  libstratego-xtc // TODO2: XTC detox
  libstrc

strategies
  
  // We parse individual files using libstrc.
  // Using pack-stratego isn't really an option as long as
  // we want to support both .str/.rtree and .astr files
  
  // FIXME: Proper -I path for parse table
  
  input-stratego-file =
    xtc-ensure-file => FILE(file);
    parse-stratego-trm(
    | [ "-i", file
      , "-I", "syntax", "-I", "../syntax"
      , "--default-syntax", "Stratego-Attributes"
      ]
    )

  output-stratego-file =
    write-to;
    xtc-transform(!"pp-stratego", !["--abstract" | <pass-verbose>])
  <+
    pp-format-checker-error(|"Internal error: generated AST is malformed", "Stratego-Sugar.rtg")

   pp-format-checker-error(|msg, rtg) =
      err-msg(|msg)
    ; write-to => FILE(file)
    ; try(<xtc-command(!"format-check")> ["--vis", "-i", file, "--rtg", <xtc-find> rtg])
    ; log(|Critical(), "Fatal errors encountered in generated code")
    ; <xtc-exit> 1
  
  parse-table-stratego-attributes =
    CachedParseTableStrategoAttributes
  <+
    table := <open-parse-table> <import-term(Stratego-Attributes.tbl)>;
    rules(CachedParseTableStrategoAttributes := table)
