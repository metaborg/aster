module default-attributes

imports
  Stratego-Attributes
  stratego-rerag

strategies

  // TODO2: Check for zero params
  is-native-attribute = "parent" + "init" + "uninit" + "disable-warnings"

rules

  add-default-attributes:
    Module(name, decl*) -> Module(name, [attrs | decl*])
    where
      attrs := <default-attributes>

  default-attributes = !|[
    attributes
      @local enable-warnings = rules(AstrWarningsEnabled : _)
      
      @local previous-sibling = get-previous-sibling(|id.parent)
      
      @local next-sibling = get-next-sibling(|id.parent)
      
      // TODO2: more first-child, last-child
      
      @local root = try(id.parent.root)
      
      @local debug = where(id.uninit; debug)
  
      @local path-list:
        _    -> [id.parent | id.parent.path-list]
        root -> []
  
      @local path =
         id.path-list;
         reverse;
         map(get-constructor);
         separate-by(|".");
         concat-strings
      
      @local path-short =
        id.path;
        if <gt> (<string-length>, 40) then
          string-as-chars(
            reverse;
            take(|40);
            reverse
          );
          <conc-strings> ("...", <id>)
        end
  ]|

rules // Native attribute definitions
    
  assimilate-native-ref:
    AttributeRef(t, Attribute("disable-warnings", [])) -> Strat |[ rules(AstrWarningsDisabled :- _) ]|

  assimilate-native-ref:
    AttributeRef(t, Attribute("parent", [])) -> Strat |[ <get-parent(|<all-parents>)> t ]|

  assimilate-native-ref-inside-ag(|s_parent, x_all-parents, x_current, x_attr-cache):
    AttributeRef(t, Attribute("parent", [])) -> Strat |[ s_parent ]|

  assimilate-native-ref:
    AttributeRef(t, Attribute("uninit", [])) -> Strat |[ astr-uninitialize-node(|<attribute-cache>) ]|

  assimilate-native-ref-inside-ag(|s_parent, x_all-parents, x_current, x_attr-cache):
    AttributeRef(t, Attribute("uninit", [])) -> Strat |[ astr-uninitialize-node(|x_attr-cache) ]|
  
  assimilate-native-ref:
    AttributeRef(t, Attribute("init", [])) -> Strat |[ <astr-initialize-node(|<all-parents>)> t ]|
  
  assimilate-native-ref-inside-ag(|s_parent, x_all-parents, x_current, x_attr-cache):
    AttributeRef(t, Attribute("init", [])) -> Strat |[ <astr-initialize-node(|x_all-parents)> t ]|
