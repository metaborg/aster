module back/rule-definitions

imports
  Stratego-Attributes
  stratego-rerag
  back/default-attributes
  back/util
  back/rule-merging
  back/rule-defaults
  back/rule-naming
  back/attribute-references

overlays

  DEFAULT_ARG_COUNT = 4

strategies

  // TODO: Reconsider hard failure for attribute strategies
  //       id.ancestor(p) = p <+ id.ancestor(p) isn't nice to write,
  //       but it's consistent with the := operator

  // TODO2: runtime check for anotated terms used for/by dynamic rules
  
  default-params =
    with(
      x_parent       := <ParentVar>;
      x_all-parents  := <AllParentsVar>;
      x_attr-cache   := <AttrCacheVar>;
      x_rewrite-mode := <RewriteModeVar>
    );
    
    !Typedid* |[ x_parent, x_all-parents, x_attr-cache, x_rewrite-mode ]|
  
  default-args =
    default-params;
    params-to-vars

  assimilate-def:
    AttributeDef(
      Type(type, decorators, _)
    , t_pattern
    , AttributeDefStrategy(child, attr @ Attribute(name, sa*, a*), s)
    ) ->
    <make-merged-def(|t_pattern, child)>
    Def |[
      x_eval(sa* |x_parent, x_all-parents, x_attr-cache, x_rewrite-mode, a'*) =
        ?x_node;
        s_pattern;
        where(s_init-args); // TODO: init args first if built-in pattern?
        s'
    ]|
    where
      <not(Def)> type
    with
      x_parent           := <ParentVar>;
      x_node             := <NodeVar>;
      x_all-parents      := <AllParentsVar>;
      x_attr-cache       := <AttrCacheVar>;
      x_rewrite-mode     := <RewriteModeVar>;
      s_pattern          := <assimilate-pattern(|child)> t_pattern;
      type-name          := <attribute-type-name(|type)> attr;
      sig                := <attribute-signature> attr;
      x_eval             := <attribute-accessor-name> attr;
      (a'*, s_init-args) := <extract-arg-patterns> a*;
     
      s_parent         := |[ ?x_node; !x_parent; not(UnknownParent)
                             <+ get-parent(|x_all-parents) ]|;
      s_parent-desired := |[ ?x_node < !x_parent + !UnknownParent() ]|;
      
      !s;
      topdown(try(
        assimilate-expr-inside-ag(|sig, s_parent, s_parent-desired, x_node, x_all-parents, x_attr-cache)
      ));
      add-eval-failed => s'
    
  assimilate-def:
    AttributeDef(Type(Def(), _, _), _, _) -> []
  
  assimilate-where-with =
    map(
      ?Where(s)
    + \With(s) -> Strat |[ with'(s) ]|\
    );
    fold-seq

rules // Argument patterns
   
  extract-arg-patterns =
    unzip(extract-arg-pattern);
    (id, fold-seq)
  
  extract-arg-pattern:
    t -> (DefaultVarDec(x_name), |[ !x_name => t' ]|)
    with
      t'     := <alltd(match-var-ignore-annos)> t; // don't care about annos
      x_name := <newname> "arg"
  
  match-var-ignore-annos:
    Var(x) ->
    |[ <(id < id + ?x); // x may be bound or not; avoid compiler complaints
        (?x <+ eq-ignore-annos(|x))>
    ]|

rules // Semantic rule definition helpers

  // TODO: Strategies should always cause "soft" failure, mainly to support stacked decorators

  add-eval-failed:
    InlineWhereClause(c, s) -> |[ where(c'); (s <+ !EVAL_FAILED()) ]|
    with
      c' := <assimilate-where-with> c
  
  add-eval-failed:
    s -> |[ s <+ !EVAL_FAILED() ]|
    where
      not(?InlineWhereClause(_, _))
   
  assimilate-pattern(|child) =
    ?t_pattern;
    switch !child
      case Root() := t_pattern:
        !|[ where(~<root-pattern>) ]|
      
      case NoChild:
        !|[ where(?t_pattern) ]|
      
      case ?Child(child'):
         // if the attribute is specified on a pattern child, it becomes the current term (x_node)
         <ancestor-pattern(|child')> t_pattern
    end
   
  root-pattern =
    with(
      x_parent      := <ParentVar>;
      x_all-parents := <AllParentsVar>
    );
    !|[ ?root;
        if UnknownParent() := x_parent then
          get-parent(|x_all-parents); NoParent
        else
          <NoParent> x_parent
        end
    ]|
  
  /**
   * Match based on an ancestor (e.g., for def td Foo(Bar(x)).x).
   */
  ancestor-pattern(|x_child):
    t_pattern ->
    |[ ?x_child;
       (!x_parent; not(UnknownParent + NoParent) <+ s_traverse-up);
       s_traverse-up' => t_pattern
    ]|
    with
      x_parent       := <ParentVar>;
      x_all-parents  := <AllParentsVar>;
      depth          := <dec> <term-depth(|Var(x_child))> t_pattern;

      if -1 := depth then // pff.. recover if the child IS the pattern
        s_traverse-up  := Id();
        s_traverse-up' := |[ !x_child ]|
      else
        s_traverse-up  := |[ get-parent(|x_all-parents); not(NoParent) ]|;
        s_traverse-up' := <copy; fold-seq> (depth, s_traverse-up)
      end
    
    // TODO2: Optimize - ancestor-check should use intermediate patterns

