/**
 * Aster decorator definition assimilation.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module decorator-definitions

imports
  libstratego-lib
  front/analyze
  back/equation-definitions
  back/decorator-references

overlays

  DECORATOR_SARG_COUNT = 1 // added system strategy args for decorator strategies
  DECORATOR_TARG_COUNT = 1 // added system term args for decorator strategies

rules // Assimilation of decorator definitions
  
  assimilate-decorator-def =
    ADef(
      Type(Decorator(), id, id)
    , id
    , ADefStrategy(
        id
      , Attribute(
          ?x_decorator; DecoratorAccessorName
        , ?[a_decorated | as*];
          ![DefaultVarDec(<CachedVar>) | <id>]
        , ?a*;
          ![Var(<SignatureVar>) | a*]
        ) => attr
      , assimilate-decorator-body(|attr, x_decorator, as*, a*, a_decorated)
      )
    );
    attribute-signature-wrap(
      assimilate-def
    | Var(<SignatureVar>)
    )
 
  assimilate-decorator-body(|attr, x_decorator, as*, a*, a_decorated):
    s_body ->
    |[ (id < id + ?x_decorated-value);
       s_body'
    ]|
    with
      x_decorated-value := <DecoratedValueVar>;
      x_decorated       := <param-to-string> a_decorated;
      (s_arg*, t_arg*)  := <params-to-vars> (as*, a*);

      attribute-signature-wrap(
        decorated-attribute-name-wrap(
          !s_body;
          
          topdown(try(
            assimilate-decorator-self-ref(|x_decorated, x_decorator, s_arg*, t_arg*)
          <+
            assimilate-decorated-svar(|attr, x_decorated)
          <+
            assimilate-native-decoration-ref(|x_decorated)
          <+
            assimilate-expr-inside-decorator
          )) => s_body'
        | x_decorated
        )
      | Var(<SignatureVar>)
      )

  decorated-attribute-name-wrap(s |x_decorated) =
    {| DecoratedAttributeSignature:
      rules(
        DecoratedAttributeSignature: |[ x_decorated(|) ]| -> Var(<SignatureVar>)
      );
      s
    |}

  assimilate-decorated-svar(|attr, x_decorated):
    |[ x_decorated(|) ]| ->
    |[ if ?x_node then
         !x_cached <+ x_decorated => x_cached
       else
         x_decorated
       end;
       not(EVAL_FAILED)
    ]|
    with
      x_node   := <NodeVar>;
      x_cached := <DecoratedValueVar>
