/**
 * Attribute memoization.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module back/attribute-caching

imports
  libstratego-lib
  back/util

rules

  add-cached-rule-logic(|def):
    |[ x_eval(sa*|a*) =
         s_rewrite => x_node;
         s_body
    ]| ->
    |[ x_eval(sa*|a*) =
         s_cache-read
         < not(EVAL_FAILED)
         + s_rewrite => x_node;
           (s_body <+ !EVAL_FAILED());
           
           if not(EVAL_FAILED) then
             aster-init-node-ensure(|x_all-parents, x_global-cache)
           end;
           s_cache-write
    ]|
    where
      ([], a*) := <RuleParams> def; // no strategy args
      not(has-plain-decorator);
      <has-foreign-operations> def
    with
      ([], t*)        := <params-to-vars> ([], a*);
      s_cache-read    := <add-cache-read(|t*)> def;
      s_cache-write   := <add-cache-write(|t*)> def;
      x_all-parents   := <AllParentsVar>;
      x_global-cache  := <GlobalCacheVar>
  
  add-uncached-rule-logic:
    |[ x_eval(sa*|a*) = s ]| ->
    |[ x_eval(sa*|a*) =
         s;
         (EVAL_FAILED <+ s_init)
    ]|
    with
      if has-plain-decorator + not(has-foreign-operations) then
        s_init := Id()
      else
        x_all-parents  := <AllParentsVar>;
        x_global-cache := <GlobalCacheVar>;
        s_init := |[ aster-init-node-ensure(|x_all-parents, x_global-cache) ]|
      end

strategies

  add-cache-read(|sargs, targs) =
    if [] := sargs then
      add-cache-read(|targs)
    else
      !Fail()
    end

  add-cache-write(|sargs, targs) =
    if [] := sargs then
      add-cache-write(|targs)
    else
      !Id()
    end
  
  add-cache-read(|targs):
    def ->
    |[ get-cached-attribute(|x_global-cache, <id>, t_key) ]|
    with
      x_all-parents   := <AllParentsVar>;
      x_global-cache  := <GlobalCacheVar>;
      t_key           := <make-cache-key(|targs)> def

  add-cache-write(|targs):
    def ->
    |[ set-cached-attribute(|x_global-cache, x_node, t_key, <id>) ]|
    with
      x_global-cache  := <GlobalCacheVar>;
      x_node          := <NodeVar>;
      t_key           := <make-cache-key(|targs)> def

  make-cache-key(|targs):
    def @ Attribute(name, sarity, tarity) ->
    <try(apply-alter-signature-decorator(|def))> Term |[
      Attribute(~str:name, t_s, t_t)
    ]|
    with
      t_s    := NoAnnoList(List(<copy> (sarity, Term |[ Anonymous() ]|)));
      targs' := <drop(|DEFAULT_ARG_COUNT())> targs;
      t_t    := NoAnnoList(List(targs'))
