/**
 * Rule tracing.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module back/rule-tracing

imports
  libstratego-lib
  back/util
  back/default-attributes
  back/attribute-references
  front/analyze

strategies

  trace-rule =
    trace-rule(|"")
  
  trace-default-rule =
    trace-rule(|" (copy rule)")
  
  trace-failed-rule =
    trace-rule(|" (failed)")
  
  trace-fixpoint-rule =
    trace-rule(|" (fixpoint)")
  
  trace-rule(|comment) =
    where(Attribute(real-name, _, _) := <attribute-signature>);
    
    if <get-config> Trace(); not(<ignore-trace-def> real-name) then
      trace-rule(|comment, real-name)
    else
      !Strat |[ id ]|
    end
  
  trace-rule(|comment, real-name):
    def @ Attribute(name, sarity, tarity) ->
    Strat |[
        say(<conc-strings> (
             ~str:message
           , <astr-node-path(|x_parent, x_all-parents, x_attr-cache)>
           , t_comment*
        ))
      ]|
    with
      decorators    := <get-attribute-decorators> def;
      decorators'   := <separate-by(|" "); concat-strings> decorators;
      arity'        := <arity-to-string> (sarity, tarity);
      message       := <conc-strings> ("  [trace] ", decorators', " ", real-name, arity', " @ ");
      x_parent      := <ParentVar>;
      x_all-parents := <AllParentsVar>;
      x_attr-cache  := <AttrCacheVar>;
      t_arg*        := <default-args>;
      
      if "" := comment then
        t_comment* := []
      else
        t_comment* := [Term |[ ~str:comment ]|]
      end
  
  trace-rewrite =
    if <get-config> Trace() then
      !Strat |[
        say(<conc-strings> (
          "  [rewr.] "
        , <astr-node-path(|UnknownParent(), <all-parents>, <attr-cache>)> ~id:<NodeVar>
        , " -> "
        , <to-tiny-string>
        ))
      ]|
    else
      !Strat |[ id ]|
    end
