/**
 * Aster native attribute definitions.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module back/attribute-builtins

imports
  Stratego-Attributes
  aster
  front/analyze

rules // Native attribute definitions

  // Native attributes may work on non-annotated terms, or perform
  // special or optimized operations not directly supported by regular
  // attributes.
  
  is-native-attribute =
    where(assimilate-native-attr)

  assimilate-native-ref:
    AttributeRef(t, attr) -> |[ !t; s ]|
    where
      s := <assimilate-native-attr> attr

  assimilate-native-attr:
    Attribute("parent", [], []) ->
    |[ get-parent(|<all-parents>); not(NoParent) ]| // optimized in equation-postprocess
  
  assimilate-native-attr:
    Attribute("eval", [s, s-default], []) -> |[ fatal-err(|"id.eval() not fully implemented yet") ]|
  
  assimilate-native-attr:
    Attribute("signature", [], []) -> |[ !t_signature => Attribute(_, [], _) ]|
    with
      t_signature := <CurrentSignatureTerm <+ !|[ <fail> ]|>
    
  assimilate-native-attr:
    Attribute("attribute-name", [], []) -> |[ !t_signature => Attribute(<id>, _, _) ]|
    with
      t_signature := <CurrentSignatureTerm <+ !|[ <fail> ]|>
    
rules // Native attributes on decorated evaluation functions

  is-native-attribute = is-decoration-native-attr

  // TODO2: Proper native attr checks for decoration attrs  
  is-decoration-native-attr =
    where(assimilate-decoration-native-attr(|"d"))

  assimilate-native-decoration-ref(|x_decorated):
    AttributeRef(Var(x_decorated), attr) -> <assimilate-decoration-native-attr(|x_decorated)> attr
  
  assimilate-decoration-native-attr(|x_decorated):
    Attribute("defined", [], []) ->
    |[ where(
         ?x_node
         < !x_cached <+ x_decorated => x_cached
         + x_decorated
       )
    ]|
    with
      x_cached := <DecoratedValueVar>;
      x_node   := <NodeVar>
  
  assimilate-decoration-native-attr(|x_decorated):
    Attribute("attribute-name", [], []) -> |[ !x_signature => Attribute(<id>, _, _) ]|
    with
      x_signature := <SignatureVar>
  
  assimilate-decoration-native-attr(|x_decorated):
    Attribute("signature", [], []) -> |[ !x_signature => Attribute(_, [], _) ]|
    with
      x_signature := <SignatureVar>
