module back/attribute-references

imports
  Stratego-Attributes
  stratego-rerag
  back/default-attributes
  back/rule-naming

rules // Evaluate attributes in AG context

  assimilate-expr-inside-ag(|sig, s_parent, s_parent-desired, x_node, x_all-parents, x_attr-cache) =
    assimilate-native-ref-inside-ag(|s_parent, x_all-parents, x_node, x_attr-cache)
  <+
    assimilate-native-ref
  <+
    ?AttributeRef(_, attr);
    assimilate-attr-ref-inside-ag(|s_parent, s_parent-desired, x_node, x_all-parents, x_attr-cache);
    try(
      assimilate-attr-self-ref-inside-ag(|sig, attr, s_parent, x_node)
    )
  
  // TODO: Eliminate s_parent-desired param
  assimilate-attr-ref-inside-ag(|s_parent, s_parent-desired, x_node, x_all-parents, x_attr-cache):
    AttributeRef(t, attr @ Attribute(name, s*, t*)) ->
    |[ <x_eval(s* |<s_parent-desired>, x_all-parents, x_attr-cache, x_rewrite-mode, t*)> t ]|
    where
      not(<is-decorator> attr)
    with
      !attr;
      x_eval         := <attribute-accessor-name> attr;
      x_rewrite-mode := <RewriteModeVar>

  // TODO2: Optimize - don't redirect attribute self-references for child terms
  //       	e.g., syn Fork(t1, t2).min := <min> (t1.min, t2.min)

  // TODO: Don't redirect attribute self-references, at all? (also see rule-definitions xdoc, todos)

  /**
   * Redirect attribute self-references.
   *
   * (e.g., 'def down foo = id.foo' effectively becomes 'def down foo = id.parent.foo').
   */
  assimilate-attr-self-ref-inside-ag(|sig, attr, s_parent, x_node):
    |[ <s> t ]| ->
    |[ <?x_node < warn(|~str:message); s_eval-default + s> t ]|
    where
      <attribute-signature> attr => sig
    with
      s_eval-default := <default-rule-call> attr;
      name           := <?Attribute(<id>, _, _)> sig;
      message        := <conc-strings> ("self-reference is not allowed in attribute ", name)

rules // Evaluate attributes outside of AG context

  // TODO2: Never eagerly get parent reference?

  assimilate-expr =
    assimilate-native-ref
  <+
    assimilate-attr-ref
  <+
    assimilate-decorator-ref

  // TODO2: Optimize - use local variables to cache all <attr-cache> and <all-parents> calls

  assimilate-attr-ref:
    AttributeRef(t, attr @ Attribute(name, s*, t*)) ->
    Strat |[
      !t;
      astr-initialize-node-ensure(
        astr-initialize-node-warning(|x_all-parents, ~str:name')
      | <all-parents => x_all-parents>
      , <attr-cache => x_attr-cache>
      );
      x_eval(s* |UnknownParent(), x_all-parents, x_attr-cache, <get-rewrite-mode(|x_attr-cache)>, t*)
    ]|
    where
      not(<is-decorator> attr)
    with
      x_eval         := <attribute-accessor-name> attr;
      x_all-parents  := <AllParentsVar>;
      x_attr-cache   := <AttrCacheVar>;
      name'          := name

