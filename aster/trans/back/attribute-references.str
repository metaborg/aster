/**
 * Attribute references.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module back/attribute-references

imports
  Stratego-Attributes
  aster
  back/attribute-builtins
  back/decorator-references

rules // Evaluate attributes in AG context

  assimilate-expr-inside-ag =
    assimilate-native-ref
  <+
    assimilate-decorator-ref(assimilate-expr-inside-ag)
  <+
    assimilate-attr-ref-inside-ag
  
  assimilate-attr-ref-inside-ag:
    AttributeRef(t, attr @ Attribute(name, s*, t*)) ->
    |[ <x_eval(s* |< ?x_node < !x_parent + !UnknownParent()>, x_all-parents, x_global-cache, x_rewrite-mode, t*)> t;
       not(EVAL_FAILED)
    ]|
    where
      not(<is-decorator> attr)
    with
      !attr;
      x_parent       := <ParentVar>;
      x_all-parents  := <AllParentsVar>;
      x_global-cache := <GlobalCacheVar>;
      x_node         := <NodeVar>;
      x_rewrite-mode := <RewriteModeVar>;
      x_eval         := <attribute-accessor-name> attr

rules // Evaluate attributes outside of AG context

  assimilate-expr-outside-ag =
    assimilate-native-ref
  <+
    assimilate-attr-ref-outside-ag
  <+
    assimilate-decorator-ref(assimilate-expr-outside-ag)

  /**
   * Assimilate an expression inside AG context,
   * doing an initialization sanity check before
   * evaluating the attribute.
   */
  assimilate-attr-ref-outside-ag:
    AttributeRef(t, attr @ Attribute(name, s*, t*)) ->
    |[
      (id < id + ?x_all-parents; ?x_global-cache; ?x_rewrite-mode); // never consider these unbound
      !t;
      aster-init-node-ensure(
        s_warning
      | <!x_all-parents <+ all-parents => x_all-parents>
      , <!x_global-cache <+ global-cache => x_global-cache>
      );
      x_eval(
        s*
      | UnknownParent(), x_all-parents, x_global-cache
      , <!x_rewrite-mode <+ get-rewrite-mode(|x_global-cache) => x_rewrite-mode>
      , t*
      );
      not(EVAL_FAILED)
    ]|
    where
      not(<is-decorator> attr)
    with
      x_eval         := <attribute-accessor-name> attr;
      x_all-parents  := <AllParentsVar>;
      x_global-cache := <GlobalCacheVar>;
      x_rewrite-mode := <RewriteModeVar>;
      name'          := name;
      
      if <has-marker-decorator(|DISABLE_WARNINGS_DECORATOR())> attr then
        s_warning := Id()
      else
        s_warning := |[ aster-initialize-node-warning(|x_all-parents, ~str:name') ]|
      end

