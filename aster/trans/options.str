/**
 * Aster options.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module options

imports
  libstratego-lib

signature constructors

  Trace : OptionKey

strategies
  
  aster-options =
    Option(
      "--trace"
    , <set-config> (Trace(), <id>)
    , !"--trace          Enable tracing messages for all attributes (for debugging support)"
    )
  + ArgOption(
      "-I" + "--Include"
    , <post-extend-config> ("-I", ["-I", <id>])
    , "-I d | --Include d Include modules from directory d"
    )
  
  infer-output-option =
    <get-config> "--multi-output";
    if all(has-extension(|"rtree")) then
      rules(OutputAST: _)
    else
      if not(all(has-extension(|"str"))) then
        fatal-err(|"Please specify output files with a .str or .rtree extension")
      end
    end
  <+
    <get-config> "--multi-input";
    map(guarantee-extension(|"rtree"));
    <set-config> ("--multi-output", <id>);
    rules(OutputAST: _)
  <+
    ![stdout()]
