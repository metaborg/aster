module desugar

imports
  libstratego-lib
  Stratego-Attributes
  aster

signature constructors
  // used in intermediate syntax
  APDefStrategy     : Pattern * OptDotChild * Strategy -> PatternDefBlockDef
  InlineWhereClause : List(Strategy) * Strategy        -> Strategy
rules

  desugar-all =
    topdown(repeat(
      desugar-attribute-ref
    + desugar-attribute-def
    + desugar-decorator-ref
    + desugar-id-term
    + desugar-fail-term
    + desugar-attribute-child
    + desugar-application
    + desugar-build-apply
    ));
    
    try(
      all-stratego-defs(
        try(aster-flatten-block-def);
        try(
          desugar-scope-default + map(desugar-scope-default)
        )
      )
    )

rules

  desugar-attribute-def:
    Attributes(def*) -> Rules(def*)

  desugar-attribute-def:
    Decorators(def*) -> Rules(def*)
     
  desugar-attribute-def:
    APDefTerm(pattern, child, term, condition) ->
    APDefStrategy(pattern, child, s)
    with
      s := InlineWhereClause(condition, Build(term))
  
  desugar-attribute-def:
    ADefTerm(child, attribute, term, condition) ->
    ADefStrategy(child, attribute, s)
    with
      s := InlineWhereClause(condition, Build(term))
  
  desugar-attribute-def:
    ADefNoPattern(type, value) ->
    ADef(type, Wld(), value)

rules

  // Desugar Stratego-Sugar a bit for decorator svars

  desugar-application:
    Strat |[ x ]| -> |[ x(|) ]|

  desugar-application:
    Strat |[ x() ]| -> |[ x(|) ]|

rules

  desugar-attribute-ref:
    AttributeNoArgs(x) -> Attribute(x, [], [])
  
  desugar-attribute-ref:
    AttributeNoTerms(x, s*) -> Attribute(x, s*, [])

  desugar-decorator-ref:
    DecoratorNoArgs(x) -> Decorator(x, [], [])
  
  desugar-decorator-ref:
    DecoratorNoTerms(x, s*) -> Decorator(x, s*, [])
  
  desugar-attribute-ref:
    AttributeTerm(x) -> RootApp(x)
  
  desugar-attribute-child:
    DotChild(x) -> Child(x)
  
  desugar-attribute-child:
    DotIdChild() -> IdChild()
  
  desugar-attribute-child:
    IdChild() -> NoChild()
  
  desugar-fail-term:
    FailTerm() -> RootApp(Fail())
  
  desugar-id-term = // match context
    at-match-all(
      \NoAnnoList(IdTerm()) -> Wld(){IdTerm()}\ // annotate for IdTerm warning
    + \IdTerm() -> Wld(){IdTerm()}\
    )
  
  desugar-id-term: // build context
    NoAnnoList(IdTerm()) -> RootApp(Id())
  
  desugar-id-term: // build context (AnnoList)
    IdTerm() -> RootApp(Id())

  at-match-all(s) =
    ?before;
    at-match(
      rec rec(
        s
      <+
        RootApp(id) <+ App(id, rec)
      <+
        all(rec)
      )
    );
    not(?before)

  at-match(s) =
    Match(s) + NameDefBlock(id, s, id) + RuleNoCond(s, id) + Rule(s, id, id) + AM(id, s) + Assign(s, id)
  + ADefStrategy(id, Attribute(id, id, map(s)), id) + AnonymousRewriteRule(s, id, id)

  desugar-build-apply:
    Build(App(strategy, term)) -> BA(strategy, term)
  
  desugar-build-apply:
    Build(NoAnnoList(RootApp(apply))) -> apply
  
  desugar-build-apply:
    Build(NoAnnoList(AttributeTerm(apply))) -> apply

rules

  /**
   * Avoid "parent0 not found" error in "{ id.parent }"
   * by desugaring variable scopes.
   */
  desugar-scope-default =
    where(
      ?RDefNoArgs(_, _)
    + ?RDef(_, _, _)
    + ?RDefT(_, _, _, _)
    + ?SDefNoArgs(_, _)
    + ?SDef(_, _, _)
    + ?SDefT(_, _, _, _)
    + ?ADef(_, _, _)
    + ?ADecl(_, _)
    );
    desugar-scope-default-in-def
 
  desugar-scope-default-in-def =
    oncetd(?ScopeDefault(_));
    with(
      scopeable := <collect-all(try(param-to-var); ?Var(<id>))>
    );
    topdown(try(
      \ScopeDefault(s) -> Scope(scopeable, s)\
    ))
     
rules // flatten block definitions to individual definitions

  aster-flatten-block-def =
    ?NameDefBlock(type, pattern, <id>);
    map(
      \ADefStrategy(child, attr, s) ->
       ADef(type, pattern, ADefStrategy(child, attr, s))\
    )
  
  aster-flatten-block-def =
    ?PatternDefBlock(type, attr, <id>);
    map(
      \APDefStrategy(pattern, child, s) ->
       ADef(type, pattern, ADefStrategy(child, attr, s))\
    )

  aster-flatten-block-def =
    ?ADecl(type, <id>);
    where(?[_, _ | _]);
    map(
      !ADecl(type, [<id>])
    )
