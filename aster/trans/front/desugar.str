module desugar

imports
  libstratego-lib
  Stratego-Attributes
  stratego-rerag

signature constructors
  // used in intermediate syntax
  AttributePDefStrategy : Pattern * OptDotChild * Strategy -> PatternDefBlockDef

rules

  desugar-all =
    topdown(repeat(
      desugar-attribute-ref
    + desugar-attribute-def
    + desugar-where-with
    + desugar-id-term
    + desugar-fail-term
    + desugar-attribute-child
    + desugar-application
    ));
    
    try(
      all-stratego-defs(
        try(astr-flatten-block-def)
      )
    )

rules

  desugar-attribute-def:
    Attributes(def*) -> Rules(def*)
  
  desugar-attribute-def:
    AttributeDeclNoPattern(type, attr, circular) ->
    AttributeDecl(type, Id(), attr, circular)
     
  desugar-attribute-def:
    AttributePDefTerm(pattern, child, term, condition) ->
    AttributePDefStrategy(pattern, child, s)
    where
      s := Seq(condition, Build(term))
  
  desugar-attribute-def:
    AttributeDefTerm(child, attribute, circular, term, condition) ->
    AttributeDefStrategy(child, attribute, circular, s)
    where
      s := Seq(condition, Build(term))
  
  desugar-attribute-def:
    AttributeDefNoPattern(type, value) ->
    AttributeDef(type, Wld(), value)
  
  desugar-attribute-def:
    CircularFail() -> Circular(FailTerm())

rules // where and with clause

  // These are inlined as strategies above,
  // but only the WhereClause remains in the core language
  // for special treatment by the assimilation phase.
  
  desugar-where-with:
    Seq(WithClause(s1), s2) -> |[ with'(s1); s2 ]|
  
  desugar-where-with:
    NoWhere() -> Id()
  
  desugar-where-with:
    Seq(WhereWithClause(s1, s2), s3) -> Seq(WhereClause(s1), |[ with'(s2); s3 ]|)
  
  desugar-where-with:
    AnonymousRewriteRuleNoWhere(t1, t2) -> AnonymousRewriteRule(t1, t2, Id())

rules

  // Desugar Stratego-Sugar a bit for decorator svarss

  desugar-application:
    Strat |[ x ]| -> |[ x(|) ]|

  desugar-application:
    Strat |[ x() ]| -> |[ x(|) ]|

rules

  desugar-attribute-ref:
    AttributeNoArgs(x) -> Attribute(x, [], [])
  
  desugar-attribute-ref:
    AttributeNoTerms(x, s*) -> Attribute(x, s*, [])
  
  desugar-attribute-ref:
    AttributeTerm(x) -> RootApp(x)
  
  desugar-attribute-child:
    DotChild(x) -> Child(x)
  
  desugar-attribute-child:
    DotIdChild() -> IdChild()
  
  desugar-attribute-child:
    IdChild() -> NoChild()
  
  desugar-fail-term:
    FailTerm() -> RootApp(Fail())
  
  desugar-id-term = // match context
    at-match-all(
      \NoAnnoList(IdTerm()) -> Wld()\
    + \IdTerm() -> Wld()\
    )
  
  desugar-id-term: // build context
    NoAnnoList(IdTerm()) -> RootApp(Id())
  
  desugar-id-term: // build context (AnnoList)
    IdTerm() -> RootApp(Id())

  at-match-all(s) =
    ?before;
    at-match(
      rec rec(
        s
      <+
        RootApp(id) <+ App(id, rec)
      <+
        all(rec)
      )
    );
    not(?before)

  at-match(s) =
    Match(s) + NameDefBlock(id, s, id) + RuleNoCond(s, id) + Rule(s, id, id) + AM(id, s) + Assign(s, id)
  + AttributeDefStrategy(id, Attribute(id, id, map(s)), id) + AnonymousRewriteRule(s, id, id)
     
rules // flatten block definitions to individual definitions

  astr-flatten-block-def =
    ?NameDefBlock(type, pattern, <id>);
    map(
      \AttributeDefStrategy(child, attr, s) ->
       AttributeDef(type, pattern, AttributeDefStrategy(child, attr, s))\
    )
  
  astr-flatten-block-def =
    ?PatternDefBlock(type, attribute, circular, <id>);
    map(
      \AttributePDefStrategy(pattern, child, s) ->
       AttributeDef(type, pattern, AttributeDefStrategy(child, attribute, s))\
    )
