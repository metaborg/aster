module analyze

imports
  libstratego-lib
  Stratego-Attributes
  back/assimilate

strategies

  analyze =
    ( ?AttributeDef(type, _, AttributeDefStrategy(_, attribute, circular, s))
    + ?AttributeDecl(type, _, attribute, circular)
    );
    with(
      sig     := <attribute-signature> attribute;
      oldtype := <!sig; AttributeType <+ !type>;
      type'   := <unify-attribute-type> (type, oldtype);

      if Circular(value) := circular then
        rules(
          AttributeCircular: sig -> value
        )
      end;
      
      rules(
        AttributeType: sig -> type'
      )
    )
  
  analyze =
    ?AnonymousRewriteRule(_, _, _);
    rules(
      UsesAnonymousRewriteRules: _
    )
  
  attribute-type =
    attribute-signature;
    AttributeType
  
  // TODO2: Explicit constructors for attribute or eval signatures?
  
  attribute-signature:
    Attribute(name, s*, a*) -> <strip-annos> Attribute(name, <length> s*, <length> a*)

  attribute-signature:
    |[ x_eval(as*|a*) = s ]| -> Attribute(x_eval, sarity, tarity)
    with
      sarity := <length> as*;
      tarity := <subt> (<length> a*, AG_STANDARD_ARGS())
  
  rename-signature(|name'):
    Attribute(name,  s*, a*) ->
    Attribute(name', s*, a*)
  
  unify-attribute-type =
    \(t, t) -> t\
  <+
    \(Local(), _) -> Local()\
  <+
    \(_, Local()) -> Local()\
  <+
    \(t, NoType()) -> t\
  <+
    \(NoType(), t) -> t\
  <+
    !Chained()
    