module analyze

imports
  libstratego-lib
  Stratego-Attributes
  back/assimilate
  back/rule-naming

strategies // Analysis

  analyze =
    ( ?AttributeDef(Type(type, decorators, sort), _, AttributeDefStrategy(_, attribute, _))
    + ?AttributeDecl(Type(type, decorators, sort), attribute)
    );
    with(
      sig            := <attribute-signature> attribute;
      old-decorators := <AttributeDecorators <+ !type> sig;
      decorators'    := <unify-decorators> (decorators, old-decorators);

      if Circular(value) := circular then
        rules(
          AttributeCircular: sig -> value
        )
      end;
      
      switch !type
        case Decorator():
          rules(IsDecorator: sig)
        case Def():
          rules(HasDeclaration: sig)
      end;
      
      rules(
        AttributeDecorators: sig -> type'
      )
    )
  
  analyze =
    ?AnonymousRewriteRule(_, _, _);
    rules(
      UsesAnonymousRewriteRules: _
    )
  
  unify-decorators =
    concat // TODO: Smart decorator unification
    
strategies // Providing information
  
  get-attribute-decorators =
    attribute-signature;
    AttributeDecorators
  
  get-attribute-circular =
    attribute-signature;
    AttributeCircular

  is-decorator =
    where(
      attribute-signature;
      IsDecorator
    )

  // TODO: Explicit constructors / tuples for attribute signatures
  
  attribute-signature:
    Attribute(name, s*, a*) ->
    <strip-annos> Attribute(name, <length> s*, <length> a*)

  attribute-signature =
    Attribute(id, is-int, is-int)

  attribute-signature:
    |[ x_eval(sa*|a*) = s ]| -> Attribute(x_eval', sarity, tarity)
    with
      sarity  := <length> sa*;
      tarity  := <subt> (<length> a*, DEFAULT_ARG_COUNT());
      x_eval' := <try(UndoAccessorName)> x_eval
  
  rename-signature(|name'):
    Attribute(name,  s*, a*) ->
    Attribute(name', s*, a*)
