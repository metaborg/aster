/**
 * Aster semantic checks.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module check

imports
  libstratego-lib
  Stratego-Attributes
  front/analyze
  back/util

rules

  init-check = id.init

  check-all =
    id.init;
    collect-all(where(id.error + id.warning));
    list-loop(log-error);
   
    if one(id.error) then
      <xtc-exit> 1
    end
   
  log-error =
    with(
      context := <id.pp-context <+ !"">
    );
    with(
      log(|Error(), <conc-strings> (id.error, context), id.pp)
    <+
      log(|Warning(), <conc-strings> (id.warning, context), id.pp)
    )

attributes
  
  // TODO: use collect-bagof decorator

  def after(try(concat-strings)) error:
    Attribute(name, _, _) -> "Undefined attribute referenced"
    where
      not(get-attribute-decorators + is-native-attribute);
      not(is-decorator)
    
    Decorator(name, _, _) -> "Undefined decorator referenced"
    where
      not(get-attribute-decorators)
    
    ADef(Def(), _, ADefStrategy(_, attr, _)).attr -> "Attribute and decorator signature overlap"
    where
      <is-decorator> attr
    
    ADefStrategy(_, attr, _) -> "Attribute not declared using 'def'"
    where
      not(<HasDeclaration> <attribute-signature> attr)

    ADecl(_, [attr]) -> "No equations defined for this definition"
    where
      not(<attribute-signature; HasEquations + is-native-attribute> attr)
    
    amb(_) -> "Internal error: ambiguity in input"
    
    ADefStrategy(Child(x), _, _) -> "Child variable not defined by rule pattern"
    where
      not(id.is-pattern-var(|x))
  
    ADef(Type(Decorator(), _, _), _, ADefStrategy(_, Attribute(name, _, _), _)) -> "Illegal name for decorator"
    where
      <"def" + "eq" + "decorator"> name
  
    ADef(Type(Decorator(), _, _), _, ADefStrategy(_, Attribute(_, [], _), _)) -> "Must include attribute argument"
    
    ADefStrategy(_, Attribute(_, _, args), _) -> "Strategy applications not allowed in attribute argument patterns"
    where
      <oncetd(?RootApp(_))> args

    AttributeRef(_, _) -> "Illegal attribute reference in match pattern"
    where
      id.in-match
    
    ADef(_, Root(), ADefStrategy(Child(_), _, _)) -> "Illegal child pattern on root node definition"
    
    ADefStrategy(_, attr, _) -> "Cannot redefine a native attribute"
    where
      <is-native-attribute> attr;
      not(is-decoration-native-attr)
    
    ADefStrategy(_, attr, _) -> ["Context-free attribute illegally references parent from '", ref.pp-sig, "'"]
    where
      <has-marker-decorator(|CONTEXT_FREE_DECORATOR())> attr;
      ref := <find-parent-attribute-use> attr

    AttributeRef(not-Id, Attribute("attribute-name", [], [])) -> "Native attribute 'attribute-name' can only be referenced as 'id.attribute-name' in attributes, or on a decorated argument"
    where
      if id.in-decorator-def then
        not(
          Var(v) := not-Id;
          id.is-decorated-arg(|v)
        )
      else
        not(
          id.in-attribute-def;
          <RootApp(Id())> not-Id
        )
      end

    AttributeRef(not-Id, Attribute("signature", [], [])) -> "Native attribute 'signature' can only be referenced as 'id.signature' in attributes, or on a decorator argument"
    where
      if id.in-decorator-def then
        not(
          Var(v) := not-Id;
          id.is-decorated-arg(|v)
        )
      else
        not(
          id.in-attribute-def;
          <RootApp(Id())> not-Id
        )
      end
    
    AttributeRef(_, attr) -> ["Built-in marker decorator '", sig.pp-sig, "' cannot be used in-line"]
    where
      <is-native-decorator> attr;
      sig := <attribute-signature> attr

  def warning:
    Var(x) -> "Performing match using annotated term - use 'id.eq' instead"
    where
      id.in-match;
      not(id.in-pattern + id.in-def-param);
      (id.is-pattern-var(|x) + id.is-def-arg(|x))

    Wld(){a*} -> "Performing match using 'id' - use '_' or '<id>' instead"
    where
      <getfirst(?IdTerm())> a*
    where
      id.in-match;
      not(id.in-pattern + id.in-def-param)

    AttributeRef(not-Id, Attribute("global-cache", _, _)) -> "attribute 'global-cache' should be referenced as id.global-cache"
    where
      <not(RootApp(Id()))> not-Id

    AttributeRef(not-Id, Attribute("all-parents", _, _)) -> "attribute 'all-parents' should be referenced as id.all-parents"
    where
      <not(RootApp(Id()))> not-Id
    
    ADef(_, pattern, ADefStrategy(Child(child), _, _)).child -> "Pattern child equals pattern"
    where
      0 := <term-depth(|Var(child))> pattern
  
  def Type(_, decs, _):
    id.warning := "Definition has duplicate decorators; merging of declared decorators not is allowed"
    where
      decs.has-duplicates;
      <id.eq> (decs, id.def-type)
    
    id.error := "Definition has duplicate decorators; merging of declared decorators not is allowed"
    where
      decs.has-duplicates;
      not(<id.eq> (decs, id.def-type))
    
    id.error := "The relative ordering of decorators this definition contradicts that of another"
    where
      type   := id.def-type;
      merged := <id.uninit; ordered-union> (decs, type);
      not(<id.eq> (type, merged))
   
  def warning:
    AttributeRef(_, Attribute("cached", [_], [])) -> "Decorator 'cached' should not be used in-line"
    
    // TODO: Support in-line 'bagof' usage
    AttributeRef(_, attr) -> "'bagof' functionality not implemented for in-line decorator call"
    where
      <is-decorator> attr;
      <has-marker-decorator(|BAGOF_DECORATOR())> attr
    
  // TODO: confirm attribute applicability based on pattern, where possible
  
  // TODO: more checks
  // - congruence in attribute strategy: Foo().x = Bar() always results in a hard failure
  // - warn for "def x: x -> ... y -> ..." if x and y aren't used
  //   (should probably be := instead!)
  // - warn for rules(...id... -> foo); id does not bind to the current term but acts as a wild card
  
  // TODO: bug patterns?? maybe optional?
  // - def down is-foo - should not use 'down' without 'test' for testing attribute
  
  
  // TODO: error if an input module is never imported

strategies

  is-recursive-definition = id // TODO2: is-recursive-definition

attributes
  
  def Test down in-match:
    Match(t).t         -> t
    Assign(t, _).t     -> t
    AM(_, t).t         -> t
    As(t, _).t         -> t
    Rule(t, _, _).t    -> t
    RuleNoCond(t, _).t -> t
    RootApp(s).s       -> fail
    App(s, _).s        -> fail
    NameDefBlock(_, t, _).t      -> t
    AnonymousRewriteRule(s, _, _).s -> s
    ADefStrategy(_, Attribute(_, _, t), _).t -> t
  
  def Test down in-pattern:
    ADef(_, pattern, _).pattern -> pattern
  
  def Test down in-attribute-def:
    ADef(_, _, _) -> id
    ADecl(_, _)   -> id
  
  def Test down in-decorator-def:
    ADecl(Type(Decorator(), _, _), _)   -> id
    ADef(Type(Decorator(), _, _), _, _) -> id
  
  def Test down in-anno-pattern:
    AnnoList(_, annos).annos -> annos
  
  def Test down is-pattern-var(|var):
    ADef(_, pattern, _) -> id 
    where
      <oncetd(id.eq(|Var(var)); not(id.in-anno-pattern))> pattern
  
  def Test down is-def-arg(|x):
    ADef(_, _, ADefStrategy(_, attr, _)) -> <oncetd(try(param-to-var); id.eq(|Var(x)))> attr

  def Test down in-def-param:
    ADefStrategy(_, Attribute(_, _, t), _).t -> id
  
  def Test down is-decorated-arg(|x):
    ADef(Type(Decorator(), _, _), _, ADefStrategy(_, Attribute(_, [p | _], _), _)) -> id
    where
      x.eq(|<param-to-string> p)
  
  // TODO: Proper error pp
  def plain pp:
    _ -> id.uninit
  
  def String pp-sig:
    Attribute(name, sarity, tarity) -> <conc-strings> (name, arity)
    with
      if is-decorator; ?Attribute(_, 1, 0) then
        arity := ""
      else
        arity := <arity-to-string> (sarity, tarity)
      end
  
  def String pp-context =
    id.ancestor(?ADef(Type(Decorator(), _, _), _, <id>));
    ?ADefStrategy(_, <attribute-signature>, _);
    <conc-strings> (" in decorator '", id.pp-sig, "'")
  <+
    id.ancestor(?ADef(_, _, <id>));
    ?ADefStrategy(_, <attribute-signature>, _);
    <conc-strings> (" in attribute '", id.pp-sig, "'")
  
  def down def-type:
    ADef(_, _, ADefStrategy(_, attr, _)) ->  <get-attribute-decorators> attr.uninit
  
  def list.has-duplicates =
    not(<id.eq> (list, <id.make-set> list))
  
  // def down root.has-root-def(|name) = oncetd(ADef(...
