module assimilate

imports
  Stratego-Attributes
  stratego-rerag
  default-attributes
  util
  rule-merging
  default-rules

strategies

rules

  assimilate-all =
    add-imports;
    
    all-stratego-defs(try(
      assimilate-def
    ));
    
    topdown(try(
      assimilate-expr
    ));
    
    add-default-rules;
    add-merged-rules

  add-imports:
    Module(x, decl*) -> Module(x, [imports | decl*])
    where
      imports := |[ imports attributes parents rewrites debug ]|

  // TODO: Support args in eval functions
  // TODO: Rewrite rules
  // TODO2: runtime check for annotated terms used for/by dynamic rules

  assimilate-def:
    AttributeDef(
      type
    , t_pattern
    , AttributeDefStrategy(child, attr @ Attribute(name, args), s)
    ) ->
    <make-merged-def(|attr, t_pattern, child, type-name)>
    Def |[
      x_eval(|x_parent, x_all-parents, x_attr-cache) =
        ?x_current;
        s_pattern;
        s'
    ]|
    where
      str_name       := <conc-strings> ("\"", name, "\"");
      x_name_param   := <newname> "name";
      x_parent       := <newname> "parent";
      x_current      := <newname> "current";
      x_all-parents  := <newname> "all-parents";
      x_attr-cache   := <newname> "attr-cache";
      type-name      := <attribute-type-name(|type)> attr;
      sig            := <attribute-signature> attr;
      x_eval         := <attribute-accessor-name(|type-name)> attr;
      
      // TODO2: Better solution for passing no parent?
      s_parent         := |[ ?x_current; !x_parent; not(UnknownParent)
                             <+ get-parent(|x_all-parents) ]|;
      s_parent-desired := |[ ?x_current < !x_parent + !UnknownParent() ]|;
      
      if None() := child then
        Root() := t_pattern;
        s_pattern := |[ not(!x_parent; not(UnknownParent) <+ get-parent(|x_all-parents)) ]|
      <+
        s_pattern := |[ ?t_pattern ]|
      else // Inherited attribute
        Some(Child(child')) := child;
        s_pattern           := <ancestor-check(|t_pattern, child', x_parent, x_all-parents)>
      end;
      
      !s;
      topdown(try(
        assimilate-expr-inside-ag(|sig, s_parent, s_parent-desired, x_current, x_all-parents, x_attr-cache)
      ));
      add-eval-failed => s'
  
  add-eval-failed:
    Seq(WhereClause(s1), s2) -> |[ where(s1); (s2 <+ !EvalFailed()) ]|
  
  add-eval-failed:
    s -> |[ s <+ !EvalFailed() ]|
    where
      not(?Seq(WhereClause(c), _))
  
  /**
   * Match based on an ancestor (e.g., for @inh Foo(Bar(x)).x).
   */
  ancestor-check(|t_pattern, x_child, x_parent, x_all-parents) =
    depth          := <dec> <term-depth(|Var(x_child))> t_pattern;
    s_traverse-up  := |[ get-parent(|x_all-parents) ]|;
    s_traverse-up' := <copy; fold-seq> (depth, s_traverse-up);
    
    // TODO2: Optimize - ancestor-check should use intermediate patterns
    
    !|[ ?x_child;
        where(
          (!x_parent; not(UnknownParent) <+ s_traverse-up);
          s_traverse-up';
          ?t_pattern
        )
    ]|

strategies // Attribute strategy naming

  attribute-type-name(|local-type) =
    if NoType() := local-type then
      attribute-type // globally declared type
    else
      !local-type
    end;
    
    switch id
      case Inh():   !"inh"
      case Local(): !"local"
      otherwise:    !"syn"
    end

  attribute-accessor-name(|type):
    Attribute(name, args) -> accessor
    where
      accessor  := <conc-strings> ("eval-", type, "-", name)
  
  preferred-attribute-accessor-type =
    attribute-type-name(|NoType())

rules // Evaluate attributes in AG context

  // TODO2: Use dynamic rules to deal with with inside/outside ag rule variability

  assimilate-expr-inside-ag(|sig, s_parent, s_parent-desired, x_current, x_all-parents, x_attr-cache) =
    assimilate-native-ref-inside-ag(|s_parent, x_all-parents, x_current, x_attr-cache)
  <+
    assimilate-native-ref
  <+
    ?AttributeRef(_, attr);
    assimilate-attr-ref-inside-ag(|s_parent, s_parent-desired, x_all-parents, x_current, x_attr-cache);
    try(
      assimilate-attr-self-ref-inside-ag(|sig, attr, s_parent, x_current)
    )
   
  assimilate-attr-ref-inside-ag(|s_parent, s_parent-desired, x_all-parents, x_current, x_attr-cache):
    AttributeRef(t, attr @ Attribute(name, args)) ->
    Strat |[ <s> t ]|
    where
      type   := <preferred-attribute-accessor-type> attr;
      x_eval := <attribute-accessor-name(|type)> attr;
      if "inh" := type then
        s := |[ x_eval(|<s_parent>,         x_all-parents, x_attr-cache) ]|
      else
        // Pass current node as a parent, or pass UnknownParent
        s := |[ x_eval(|<s_parent-desired>, x_all-parents, x_attr-cache) ]|
      end

  /**
   * Adjust references to attributes to avoid circular references
   * (e.g., '@inh foo = id.foo' becomes '... = id.parent.foo').
   */
  assimilate-attr-self-ref-inside-ag(|sig, attr, s_parent, x_current):
    s_eval ->
    Strat |[
      (?x_current < s_parent + id); // 
      s_eval
    ]|
    where
      <attribute-signature> attr => sig;
      <attribute-type> attr => Inh()

rules // Evaluate attributes outside of AG context

  // TODO: Don't eagerly get parent reference

  assimilate-expr =
    assimilate-native-ref
  <+
    assimilate-attr-ref

  assimilate-attr-ref:
    AttributeRef(t, attr @ Attribute(name, args)) ->
    Strat |[
      astr-initialize-node-ensure(|<all-parents => x_all-parents>);
      <s> t
    ]|
    where
      type           := <attribute-type-name(|NoType())> attr;
      x_eval         := <attribute-accessor-name(|type)> attr;
      x_all-parents  := <newname> "all-parents";
      if "inh" := type then
        s := Strat |[ <x_eval(|<get-parent(|x_all-parents)>, x_all-parents, <attribute-cache>)> t ]|
      else
        s := Strat |[ <x_eval(|UnknownParent(),              x_all-parents, <attribute-cache>)> t ]|
      end
        
      