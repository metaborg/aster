module stratego-rerag

imports
  libstratego-lib

  front/analyze
  back/assimilate
  front/check
  front/desugar
  astr/runtime/debug
  io
  lib/xtc-multi-options

signature constructors

  Trace : OptionKey

strategies

  // TODO: Add switch for run-time checks, e.g., for circularity and tree concistency
  
  stratego-rerag =
    init-shared-vars;
    one(add-default-attributes);
    desugar-all;
    
    // init-check; // TODO: init-check, if working w/ dyn. rules...?
    all-stratego-defs(try(analyze));
    where(check-all);
    assimilate-all

strategies
 
  init-shared-vars =
    // TODO2: Things like <all-parents> should be optimized in a post-processing stage, not right away
    // TODO2: Rename attr-cache to cache or store, or something
    rules(
      ParentVar         := <newname> "parent"
      NodeVar           := <newname> "node"
      AllParentsVar     := <newname> "all-parents"
      AttrCacheVar      := <newname> "attr-cache"
      RewriteModeVar    := <newname> "rewrite-mode"
      RootVar           := <newname> "root"
      CachedVar         := <newname> "cached"
      DecoratedValueVar := <newname> "decorated-value"
    )

strategies // I/O

  main-stratego-rerag =
    xtc-multi-io-wrap(
      stratego-rerag-options
    ,
      map(input-stratego-file <+ <xtc-exit> 1);
      stratego-rerag;
      map(output-stratego-file)
    )
  
  stratego-rerag-options =
    Option(
      "--trace"
    , <set-config> (Trace(), <id>)
    , !"--trace          Enable tracing messages (for debugging support)"
    )
