module test-attributes

imports
  libstratego-lib

signature constructors
  Fork : Term * Term -> Term
  Leaf : Term        -> Term

strategies

  test-attributes =
    test-default-attributes;
    test-attribute-behavior;
    test-set-operations;
    test-reflective-attributes
    
  test-default-attributes =
    apply-and-check(
      !"id.parent (1)"
    , fetchtd(1; id.parent)
    , !Leaf(1).init
    , ?Leaf(1)
    );
    apply-and-check(
      !"id.parent (2)"
    , fetchtd(1; id.parent)
    , ![1].init
    , ?[1]
    );
    apply-and-check(
      !"id.ancestor (1)"
    , fetchtd(?1; id.ancestor(?Leaf(_)))
    , !Fork(Leaf(1), 2).init
    , ?Leaf(1)
    );
    apply-and-check(
      !"id.ancestor (2)"
    , fetchtd(1; id.ancestor(?Fork(_, _)))
    , !Fork(Leaf(1), 2).init
    , ?Fork(_, _)
    );
    apply-and-check(
      !"id.ancestor is not proper" // TODO: Should id.ancestor be proper?
    , id.ancestor(1)
    , !1.init
    , ?1
    );
    apply-and-check(
      !"id.prev-sibling"
    , fetchtd(2; id.prev-sibling(id))
    , !Fork(Leaf(1), 2).init
    , ?Leaf(_)
    )

strategies
  
  fetchtd(s) = oncetd(s; ?x); !x

attributes

  def down test-root = id.parent.test-root <+ id
  
  def circular(|0) test-silly-circular = !2
  
  def down root.test-copy-root = id
  
  def up 1.is-one = id
  
  def Fork(a@1, 2).a.child-def = id
  
  def first-kid = one(?kid); !kid
  
  def all-kids = ?_#(<id>)
  
  def all-kids-tuple = ?_#(<id>); ListToTuple
  
  def new-fork = \_#(kids) -> "Fork2"#(kids)\
  
  def x.is(|x) = id
  
  def x.tuple-first(|(x, y)) = id
  
  def Fork(a, b).b.parent-first-kid = id.first-kid
  
  def stupid.stupid.stupid = id

strategies
 
  test-attribute-behavior =
    apply-and-check(
      !"down copy rule"
    , fetchtd(1; id.test-root)
    , !Fork(Leaf(1), 2).init
    , ?Fork(_, _)
    );
    apply-and-check(
      !"circular attribute initial value"
    , id.test-silly-circular
    , 1.init
    , ?2
    );
    apply-and-check(
      !"down copy rule"
    , fetchtd(3; id.test-copy-root)
    , !Fork(1, 3).init
    , ?Fork(1, 3)
    );
    apply-and-check(
      !"up copy rule (1)"
    , id.is-one
    , !Fork(1, 3).init
    , ?1
    );
    apply-and-check(
      !"up copy rule (2)"
    , id.is-one
    , !Fork(3, 1).init
    , ?1
    );
    apply-and-check(
      !"up copy rule (3)"
    , id.is-one
    , !Some(Some(Some(1))).init
    , ?1
    );
    apply-and-check(
      !"attribute to get a child node"
    , id.first-kid
    , !Fork(1, 3).init
    , ?1
    );
    apply-and-check(
      !"reference attribute"
    , id.parent
    , !Fork(1, 3).init.first-kid // first-kid is a reference, and has a parent
    , ?Fork(1, 3)
    );
    apply-and-check(
      !"reference list attribute"
    , Hd; id.parent
    , !Fork(1, 3).init.all-kids // all-kids is a list of references, not a new tree
    , ?Fork(1, 3)
    );
    apply-and-check(
      !"reference tuple attribute"
    , Thd; id.parent
    , !Fork(1, 3).init.all-kids-tuple // all-kids is a tuple of references, not a new tree
    , ?Fork(1, 3)
    );
    apply-and-check(
      !"non-reference attribute"
    , id.new-fork.first-child.parent
    , !Fork(1, 3).init
    , not(?Fork(1, 3))
    );
    apply-and-check(
      !"id refers to the whole pattern, not the pattern child"
    , fetchtd(1; id.child-def)
    , !Fork(1, 2).init
    , ?Fork(1, 2)
    );
    apply-and-check(
      !"attribute parameter used in pattern (1)"
    , id.is(|1)
    , !1.init
    , ?1
    );
    apply-and-check(
      !"attribute parameter used in pattern (2)"
    , not(id.is(|2))
    , !1.init
    , ?1
    );
    apply-and-check(
      !"patterns in attribute arguments"
    , id.tuple-first(|(1, 2))
    , !1.init
    , ?1
    );
    apply-and-check(
      !"id.attribute does not refer to specified child"
    , fetchtd(2; id.parent-first-kid)
    , !Fork(1, 2).init
    , 1
    );
    apply-and-check(
      !"attribute on id child"
    , id.stupid
    , !1.init
    , 1
    )

  test-set-operations =
    apply-and-check(
      !"non-reference attribute"
    , id.union
    , { a := [1,2,3].init;
        b := [1,2,3].init;
        !(a,b)
      }
    , eq-ignore-annos(|[1,2,3])
    )

attributes

  def my-signature = id.signature

  def my-attribute-name = id.attribute-name

  def my-signature(s) = id.signature
  
  decorator signature-of(a) = a.signature

strategies

  test-reflective-attributes = 
    apply-and-check(
      !"id.signature"
    , id.my-signature
    , !1.init
    , Attribute("my-signature", [], [])
    );
    apply-and-check(
      !"id.attribute-name"
    , id.my-attribute-name
    , !1.init
    , "my-attribute-name"
    );
    apply-and-check(
      !"signature of remote attribute"
    , id.signature-of(id.stupid)
    , !1.init
    , Attribute("stupid", [], [])
    );
    apply-and-check(
      !"id.signature simply fails if there are strategy arguments (1)"
    , id.my-signature(id) <+ !"failed"
    , !1.init
    , "failed"
    );
    apply-and-check(
      !"id.signature simply fails if there are strategy arguments (2)"
    , id.signature-of(id.my-signature(id)) <+ !"failed"
    , !1.init
    , "failed"
    );
    apply-and-check(
      !"id.signature simply fails outside of signature context"
    , id.signature <+ !"failed"
    , !1.init
    , "failed"
    )
