/**
 * A general selection of decorators.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module aster/decorators/common

imports
  libstratego-lib
  aster/decorators/common
  aster/decorators/flow

decorators
  
  /**
   * Plain attributes do not have the default behavior of
   * caching and re-annotating a result term for re-attribution
   */
  decorator plain plain(a) = a // note that this is a special, built-in decorator
  
  decorator plain uncached(a) = a; id.ensure-init
  
  decorator plain node.uninit(a) =
    get-last-value(|id.global-cache, node, a.signature);
    not(EVAL_FAILED)
  <+
    (a <+ !EVAL_FAILED());
    id.uninit;
    set-last-value(|id.global-cache, node, a.signature, id);
    not(EVAL_FAILED)
  
  decorator test(a) =
    where(a)
  
  decorator before(a, s) =
    s; a
  
  decorator after(a, s) =
    a; s
  
  decorator in(a, s : t * s -> s) =
    s(a)
  
  /* TODO: Should `soft-try` be factored out again?
           What does that mean for decorator definitions,
           they are then basically the same as their strategy equivalents?
  
  decorator stry(a) =
    id.eval(a, id)
  */
