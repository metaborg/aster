/**
 * Assertion runtime: annotations.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 *
 * @internal
 */
module aster/lang/annotations

imports
  libstratego-lib
  aster/lang/parents
  aster/lang/lists
  aster/lang/attributes

strategies // Util

  debug'(s) =
    where(strip-annos; debug(s))

  debug' =
    where(strip-annos; debug)
  
  with'(s) =
    s
  <+
    aster-remove-annos;
    log(|Critical(), "Internal error: with clause failed unexpectedly", <id>);
    prim("SSL_stacktrace_get_all_frame_names");
    report-failure
  
  eq-ignore-annos(|term) =
    if-java-platform(
      SRTS-EXT-eq-ignore-annos(|term)
    );
    if-c-platform( // TODO2: Optimize - c-based eq-ignore-annos is expensive
      ?term
    <+
      where( // cheapish base check, must at least pass this
        is-string
      < <is-string> term
      + input-ctor := <get-constructor>{};
        term-ctor  := <get-constructor> term{};
        ?input-ctor
      );
      where(
        term' := <aster-remove-annos> term
      );
      where(
        ?term' <+ aster-remove-annos => term'
      )
    )
  
  external SRTS-EXT-eq-ignore-annos(|term)
  
  aster-is-initialized(|all-parents) = where(get-parent(|all-parents))
  
  aster-node-path(|parent, all-parents, global-cache) =
    if aster-is-initialized(|all-parents) then
      eval-path-and-node(|parent, all-parents, global-cache, 0)
    else
      <conc-strings> ("?", ".", <to-tiny-string>)
    end
  
  eval-path-and-node(|parent, all-parents, global-cache, rewrite-mode) =
    fail // generator-defined rule

strategies // Node initialization

  // TODO: Consistent naming for init and initialize functions

  // There is a bit of duplication here, since
  // this is a particularly performance-sensitive part of the code

  /**
   * Initialize an attribute value, if required, with special
   * support for reference lists and tuples.
   */
  aster-init-node-ensure(|all-parents, global-cache) =
    if not(aster-is-initialized(|all-parents)) then // be optimistic; start with this check
      // Support reference lists/tuples
      map(aster-init-single-node-ensure(|all-parents, global-cache))
    <+
      where(get-constructor => ""); // is-tuple
      all(aster-init-single-node-ensure(|all-parents, global-cache))
    <+
      aster-initialize-node(|all-parents, global-cache)
    end

  /**
   * Initialize an attribute value, if required, with special
   * support for reference lists and tuples.
   */
  aster-init-node-ensure(warning|all-parents, global-cache) =
    if not(aster-is-initialized(|all-parents)) then // be optimistic; start with this check
      // Support reference lists/tuples
      map(aster-init-single-node-ensure(warning|all-parents, global-cache))
    <+
      where(get-constructor => ""); // is-tuple
      all(aster-init-single-node-ensure(warning|all-parents, global-cache))
    <+
      where(warning);
      aster-initialize-node(|all-parents, global-cache)
    end
  
  /**
   * Initialize a node if it was not yet initialized.
   *
   * @note Generally, aster-init-node-ensure should be used instead,
   *       to support reference lists/tuples.
   */
  aster-init-single-node-ensure(|all-parents, global-cache) =
    if not(aster-is-initialized(|all-parents)) then
      aster-initialize-node(|all-parents, global-cache)
    end

  aster-init-single-node-ensure(warning|all-parents, global-cache) =
    if not(aster-is-initialized(|all-parents)) then
      where(warning);    
      aster-initialize-node(|all-parents, global-cache)
    end

  aster-initialize-node-warning(|all-parents, attribute) =
    if not(AsterWarningsDisabled <+ aster-is-reference-collection(|all-parents)) then
      warn(|["Evaluating attribute '", attribute, "' on an uninitialized term"]);
      prim("SSL_stacktrace_get_all_frame_names");
      try(\["aster_initialize_node_warning_0_0" | tail] -> tail\);
      try(\["aster_initialize_node_ensure_1_1" | tail] -> tail\);
      reverse;
      map(<fprintnl> (stderr(), ["\t", <id>]))
    end
  
  internal-enable-warnings =
    rules(AsterWarningsDisabled :- _) // ensure this rule is defined somewhere
  
  aster-is-reference-collection(|all-parents) = 
    list-loop(aster-is-initialized(|all-parents))
  <+
    where(get-constructor => ""); // is-tuple
    all(aster-is-initialized(|all-parents))
  
  aster-initialize-node-no-rewrite(|all-parents) =
    aster-initialize-node-no-rewrite(|all-parents, NoParent())
  
  aster-initialize-node-no-rewrite(|all-parents, parent) =
    // First add unique annos everywhere,
    // then assign parent rules for the newly formed nodes
    ?node;
    aster-add-annos;
    where(
      aster-add-parent-references(|all-parents, parent)
    )

strategies // Adding/removing node annotations
  
  aster-add-annos =
    leaves-consnil(
      aster-add-anno
    )

  aster-add-anno:
    t {a*} -> t {b*}
    where
      x := <if-c-platform(<newname> "@"); if-java-platform(SRTS-EXT-newint)>;
      if [] := a* then
        b* := [x] // adding annos is expensive, but newname is as cheap as a counter or new
      else
        b* := [x| <filter(not(aster-is-anno))> a*]
      end

  external SRTS-EXT-newint(|)
  
  aster-remove-annos =
    // leaves-consnil should suffice in most cases, but this
    // should handle more at about the same overhead
    topdown-consnil(aster-remove-anno)
  
  aster-remove-annos-from-leaves(|all-parents) =
    if aster-is-initialized(|all-parents) then
      leaves-consnil(aster-remove-anno)
    end
  
  aster-remove-anno =
    try(
      ?_{}
    <+
      \t {<aster-is-anno>} -> t {}\
    <+
      \t {a*} -> t {b*}
       where
         b* := <filter(not(aster-is-anno))> a*\
    )
  
  /**
   * @see aster-is-initialized
   */
  aster-is-anno =
    // TODO: Is this a sensible optimization?
    if-java-platform(
      is-int
    );
    if-c-platform(
      is-string
    // ; explode-string => ['@' | _]
    )
