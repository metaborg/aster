/**
 * Assertion runtime: anonymous rewrite rules.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 *
 * @internal
 */
module aster/lang/rewrites

imports
  libstratego-lib
  aster/lang/annotations
  aster/lang/hashtables
  aster/lang/parents
  aster/lang/lists

strategies

  get-rewrite-mode(|global-cache) =
    hashtable-get(|global-cache, "$rewrite-mode")
  <+
    !0
  
  set-rewrite-mode(|global-cache, value) =
    hashtable-put(|global-cache, "$rewrite-mode", value)
  
  get-rewrite-cached(|global-cache, node) =
    hashtable-get(|global-cache, node)

  set-rewrite-cached(|global-cache, node, value) =
    hashtable-put(|global-cache, node, value)

strategies

  eval-anonymous-rewrite = fail // this is a generated strategy
  
  aster-rewrites-enabled = fail // this is a generated strategy
  
  eval-anonymous-rewrites-cached(|all-parents, global-cache) =
    get-rewrite-cached(|global-cache, <id>)
  <+
    set-rewrite-cached(|global-cache, <id>, <id>);
    eval-anonymous-rewrites-set-cached(|all-parents, global-cache)
  
  eval-anonymous-rewrites-set-cached(|all-parents, global-cache) =
    repeat(
      { before: ?before;
        eval-anonymous-rewrite;
        persist-anonymous-rewrite(|all-parents, global-cache, before, <id>)
      }
    )

strategies

  // TODO: Optimize - don't reinitialize nodes if no attrs evaluated
  //       or check out if id.all-inline could work here

  aster-initialize-node(|all-parents, global-cache) =
    aster-initialize-node(|all-parents, global-cache, NoParent())
  
  aster-initialize-node(|all-parents, global-cache, parent) =
    if aster-rewrites-enabled then
      set-rewrite-mode(|global-cache, 1);
      do-while(
        aster-initialize-node-no-rewrite(|all-parents)
      ,
        { before:
          ?before;
          bottomup-consnil( // TODO: something like innermost-consnil?
            ( get-rewrite-cached(|global-cache, <id>)
            <+
              set-rewrite-cached(|global-cache, <id>, <id>)
            );
            eval-anonymous-rewrites-set-cached(|all-parents, global-cache)
          );
          not(?before)
        }
      );
      set-rewrite-mode(|global-cache, 0)
    else
      aster-initialize-node-no-rewrite(|all-parents, parent)
    end
  
  /**
   * Persist a rewrite in the tree by storing it in the rewrite cache.
   * Also performs this operation for all ancestor nodes.
   */
  persist-anonymous-rewrite(|all-parents, global-cache, from, to) =
    with(
      to'         := <aster-initialize-node-no-rewrite(|all-parents, global-cache)> to;
      from-parent := <get-parent(|all-parents)> from;
      to-parent   := <persist-anonymous-rewrite-upwards(|all-parents, global-cache, from, to')>;
      <set-parent(|all-parents, to-parent)> to';
      set-rewrite-cached(|global-cache, from, to')
    );
    !to'
  
  persist-anonymous-rewrite-upwards(|all-parents, global-cache, from-child, to-child):
    node -> node'
    where
      if NoParent then
        node' := node
      else
        node'       := <one-consnil((from-child -> to-child))> node;
        from-parent := <get-parent(|all-parents)> node;
        to-parent   := <persist-anonymous-rewrite-upwards(|all-parents, global-cache, node, node')>;
        
        set-rewrite-cached(|global-cache, from-parent, to-parent);
        <all-consnil(set-parent(|all-parents, to-parent))> to-parent;
        <set-parent(|all-parents, to-parent)> node'
      end
