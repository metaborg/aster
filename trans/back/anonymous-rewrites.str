module back/anonymous-rewrites

imports
  Stratego-Attributes
  aster
  back/util
  back/attribute-references

strategies
   
  // TODO2: Apply partial ordering to anon. rewr. rules?
   
  assimilate-rewrite-def:
    AnonymousRewriteRule(t_pattern, t_after, s_where) ->
    |[ eval-anonymous-rewrite =
         ?x_before;
         
         ?t_pattern;
         where(s_where');
         !t_after;
         
         not(eq-ignore-annos(|x_before));
         s_trace
    ]|
    with
      x_before := <NodeVar>;
      s_trace  := <trace-rewrite>;
      s_where' := <assimilate-where-with> s_where

  add-rewrites-enabled:
    Module(x, decl*) -> Module(x, [decl*, enabled*])
    with
      UsesAnonymousRewriteRules;
      enabled* := Def* |[ aster-rewrites-enabled = id ]|
    <+
      enabled* := []
