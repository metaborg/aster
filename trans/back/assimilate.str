/**
 * Aster main assimilation strategies.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module back/assimilate

imports
  Stratego-Attributes
  aster
  back/util
  back/equation-merging
  back/equation-definitions
  back/equation-postprocess
  back/attribute-references
  back/attribute-builtins
  back/anonymous-rewrites

strategies

  assimilate-all =
    one(try(add-imports));
    
    all-stratego-defs(try(
      assimilate-decorator-def;
      equation-postprocess-def
    <+
      assimilate-def;
      equation-postprocess-def
    <+
      assimilate-rewrite-def
    ));
    
    topdown(try(
      assimilate-expr-outside-ag
    ));
    
    one(
      add-merged-rules;
      add-rewrites-enabled
    )

  add-imports:
    Module(x, decl*) -> Module(x, [imports | decl*])
    where
      not(oncetd(Import("libstratego-lib") + Import("stratego-lib")))
    with
      imports := |[ imports libstratego-lib ]|
